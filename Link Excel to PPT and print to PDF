Sub xlToPPT()

Dim rngToCopy As Excel.Range
Dim pptApp As PowerPoint.Application
Dim pptFile As PowerPoint.Presentation
Dim pptSlide As PowerPoint.Slide
Dim myShapeRange As PowerPoint.Shape, shp As PowerPoint.Shape
Dim arrFiles As Variant


lrow = ThisWorkbook.Sheets("List").Range("B" & Rows.Count).End(xlUp).Row
arrFiles = ThisWorkbook.Sheets("List").Range("B2:E" & lrow)

With Application
  .DisplayAlerts = False
  .EnableEvents = False
  .ScreenUpdating = False
End With

For i = LBound(arrFiles) To UBound(arrFiles)

        Set wb = Workbooks.Open(arrFiles(i, 1), UpdateLinks:=False, ReadOnly:=True)
        
        'Copy Range from Excel
          Set rngToCopy = wb.Sheets(1).Range("Print_Area")
        
        'Create an Instance of PowerPoint
          On Error Resume Next
              Set pptApp = GetObject(class:="PowerPoint.Application") 'check if pptApp is already open
              Err.Clear  'Clear the error between errors
              If pptApp Is Nothing Then Set pptApp = CreateObject(class:="PowerPoint.Application") 'open pptApp is not already open
            
            'Handle if the pptApp is not found
              If Err.Number = 429 Then
                MsgBox "PowerPoint could not be found, aborting."
                Exit Sub
              End If
        
          On Error GoTo 0
          
        'Make PowerPoint Visible and Active
          pptApp.Visible = True
          pptApp.Activate
            
        'Open the Template Presentation
          pptTemp = "C:\Users\Vikram\Desktop\Presentation1.potx"
          Set pptFile = pptApp.Presentations.Open(pptTemp, msoTrue)
          Set pptSlide = pptFile.Slides(1)
        
        'Copy Excel Range
          rngToCopy.Copy
        
        'Paste to PowerPoint and position
          pptSlide.Shapes.PasteSpecial link:=True, DataType:=msoLinkedOLEObject 'ppPasteEnhancedMetafile
          Set myShapeRange = pptSlide.Shapes(pptSlide.Shapes.Count)
        'Set position:
          myShapeRange.Left = 30
          myShapeRange.Top = 90
        
        pptSlide.Shapes("Textbox 3").TextFrame.TextRange.Text = arrFiles(i, 2)
        
        Call SetExcelOLEtoManualUpdate(pptSlide)
        pptFile.PageSetup.FirstSlideNumber = arrFiles(i, 4)
        
        'Print ppt to PDF
        pptName = wb.Path & "\" & arrFiles(i, 3) & ".pptx"
        pptFile.SaveAs Filename:=pptName, FileFormat:=ppSaveAsOpenXMLPresentation
        
        pdfName = wb.Path & "\" & arrFiles(i, 3) & ".pdf"
        pptFile.SaveAs Filename:=pdfName, FileFormat:=ppSaveAsPDF
        
        wb.Close False
Next

pptApp.Quit
'Clear The Clipboard
With Application
  .CutCopyMode = False
  .DisplayAlerts = True
  .EnableEvents = True
  .ScreenUpdating = True
End With
End Sub

Function SetExcelOLEtoManualUpdate(ByRef pptSlide As PowerPoint.Slide)
    'Debug.Print shp.Name
    For Each shp In pptSlide.Shapes
    If shp.Type = msoLinkedOLEObject Then
         If shp.OLEFormat.progID = "Excel.Sheet.12" Then
            shp.LinkFormat.AutoUpdate = ppUpdateOptionManual
           End If
     End If
    Next
End Function

