1. '//Determine full path and Globally Unique Identifier (GUID)
     'to each referenced library.  Select the reference in the Tools\References
     'window, then run this code to get the information on the reference's library

	Sub ListReferencePaths()
	
	    On Error Resume Next
	    Dim ws As Worksheet, i As Long
	    Set ws = ThisWorkbook.Sheets(4)
	    
	    With ws
	        .Cells.Clear
	        .Range("A1") = "Reference name"
	        .Range("B1") = "Full path to reference"
	        .Range("C1") = "Reference GUID"
	    End With
	    For i = 1 To ThisWorkbook.VBProject.References.Count
	        With ThisWorkbook.VBProject.References(i)
	            ws.Range("A65536").End(xlUp).Offset(1, 0) = .Name
	            ws.Range("A65536").End(xlUp).Offset(0, 1) = .FullPath
	            ws.Range("A65536").End(xlUp).Offset(0, 2) = .GUID
	        End With
	    Next i
	    On Error GoTo 0
	End Sub



2. // Add a reference to the project using the GUID for the Reference library

	Sub AddReference()
	    
	        Dim strGUID() As String, theRef As Variant, i As Long
	        
	        'Update the GUID you need below.
	        ReDim strGUID(3)
	        
	        strGUID(0) = "{00020905-0000-0000-C000-000000000046}" 'Word 15
	        strGUID(1) = "{0D452EE1-E08F-101A-852E-02608C4D0BB4}" 'Forms 2.0
	        strGUID(2) = "{000204EF-0000-0000-C000-000000000046}" 'VBA 7
	        strGUID(3) = "{00020813-0000-0000-C000-000000000046}" 'Excel 15
	        
	        
	        'Set to continue in case of error
	        On Error Resume Next
	        
	        'Remove any missing references
	        For i = ThisWorkbook.VBProject.References.Count To 1 Step -1
	            Set theRef = ThisWorkbook.VBProject.References.Item(i)
	            If theRef.isbroken = True Then
	                ThisWorkbook.VBProject.References.Remove theRef
	            End If
	        Next i
	            
	        'Clear any errors so that error trapping for GUID additions can be evaluated
	        Err.Clear
	            
	        'Add the reference
	        For j = LBound(strGUID) To UBound(strGUID)
	        
	            ThisWorkbook.VBProject.References.AddFromGuid _
	            GUID:=strGUID(j), Major:=1, Minor:=0
	            
	            'If an error was encountered, inform the user
	            Select Case Err.Number
	                Case Is = 32813 'Reference already in use. No action necessary
	                Case Is = vbNullString 'Reference added without issue
	                Case Else  'An unknown error was encountered, so alert the user
	                    MsgBox "A problem was encountered trying to" & vbNewLine _
	                    & "add or remove a reference in this file" & vbNewLine & "Please check the " _
	                    & "references in your VBA project!", vbCritical + vbOKOnly, "Error!"
	            End Select
	        
	        Next j
	        
	        On Error GoTo 0
	            
	End Sub

